param (
    // Can be any of SRR, ERR, or SRX ids.
    // Pipe-separate for multiple, e.g. 'SRR1539523|SRR1539569|SRR1539570'
    sra_id string

    // S3 folder location to put the downloaded files
    output string

    // GiB of memory for samtools cat
    fastq_dump_threads = 8

    // GiB of storage for downloading SRA files (per file)
    sra_disk = 50

    // GiB of storage for converting to fastq.gz files (per file)
    fastq_dump_disk = 50
)

// Docker images
val bionode = "bionode/bionode-ncbi"
val fastq_dump = "quay.io/biocontainers/parallel-fastq-dump:0.6.3--py36_1"

// System modules included with Reflow
val dirs = make("$/dirs")
val files = make("$/files")
val strings = make("$/strings")

val stringutils = make("./../tools/stringutils.rf")


func SearchSRA(id string) =
    exec(image := bionode) (json file) {"
        bionode-ncbi search sra '{{id}}' > {{json}}
"}


// Outputs a folder with $UID/$SRA.sra, e.g.:
// $ ls -lha */*.sra
// -rw-rw-r-- 1 ubuntu ubuntu 3.6G May 16 19:57 285026/SRR629557.sra
// -rw-rw-r-- 1 ubuntu ubuntu 4.4G May 16 19:59 285027/SRR629559.sra
// -rw-rw-r-- 1 ubuntu ubuntu 4.0G May 16 20:00 285028/SRR629561.sra
// -rw-rw-r-- 1 ubuntu ubuntu 1.8G May 16 20:01 285029/SRR629562.sra
func DownloadSRA(id string) ={
    outdir := exec(image := bionode, disk := sra_disk*GiB) (outdir dir) {"
        cd {{outdir}}
        bionode-ncbi download sra '{{id}}'
    "}

    sra_files := dirs.Files(outdir)
    sra_files
}

// Convert SRA files to FastQ
// Recommended flags from https://edwards.sdsu.edu/research/fastq-dump/
// and Trinity documentation:
// > If your data come from SRA, be sure to dump the fastq file like so:
// >    SRA_TOOLKIT/fastq-dump --defline-seq '@$sn[_$rn]/$ri' --split-files file.sra
func FastqDump(sra file) = {
    outdir := exec(image := fastq_dump, cpu := 1,
                    disk := fastq_dump_disk*GiB) (outdir dir) {"
        fastq-dump --outdir {{outdir}} --gzip \
            --skip-technical  --readids --read-filter pass \
            --dumpbase --split-3 --clip --defline-seq '@$sn[_$rn]/$ri' \
            --split-files \
            {{sra}}
    "}
    x := trace(outdir)

    val (singletons, _) = dirs.Pick(outdir, "*pass.fastq.gz")
    val (R1, _) = dirs.Pick(outdir, "*pass_1.fastq.gz")
    val (R2, _) = dirs.Pick(outdir, "*pass_2.fastq.gz")

    {singletons, R1, R2}
}


func MakeRenamedFastqs(singletons, R1, R2 file, id, extension string) = {
    renamed := [stringutils.MakeFilename(id, "R1", extension): R1,
                stringutils.MakeFilename(id, "R2", extension): R2,
                stringutils.MakeFilename(id, "singletons", extension): singletons]
    dirs.Make(renamed)
}


@requires(cpu := 8)
val Main = {
    sra_ids := strings.Split(sra_id, "|")

    // Search NCBI and download metadata
    json_metadatas := [(id+"_metadata.json", SearchSRA(id)) | id <- sra_ids]
    json_dir := dirs.Make(map(json_metadatas))

    // Download SRA files
    sras := flatten([DownloadSRA(id) | id <- sra_ids])
    // create Fastq.gz files
    fastqss := [FastqDump(sra) | sra <- sras]

    fastq_dirs := [MakeRenamedFastqs(
        fastqs.singletons, fastqs.R1, fastqs.R2, id, ".fastq.gz") | (fastqs, id) <- zip(fastqss, sra_ids)]

    to_copy := fastq_dirs + [json_dir]

    [dirs.Copy(d, output) | d <- to_copy]
}
