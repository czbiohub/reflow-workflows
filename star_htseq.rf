// param (
//     // S3 path to read1 of the fastq/fasta file. If multiple files, 
//     // can be pipe-separated e.g. sample1_01.fastq|sample1_02.fastq
//     read1 string

//     // S3 path to read2 of the fastq/fasta file. If multiple files, 
//     // can be pipe-separated e.g. sample1_01.fastq|sample1_02.fastq
//     read2 string

//     // Identifier of the sample
//     name string

//     // Full s3 folder location to copy alignment output and htseq 
//     // results
//     output string

//     // Either "hg38-plus" or "mm10-plus". 
//     // Must be a .tgz file in s3://czbiohub-reference
//     genome = "hg38-plus"
// )


val files = make("$/files")
val dirs = make("$/dirs")

// Local utility file
val util = make("./util.rf")

ubuntu := "ubuntu/16.04"
star := "quay.io/biocontainers/star:2.6.0c--0"
samtools := "biocontainers/samtools"
htseq := "quay.io/biocontainers/htseq:0.9.1--py36_0"

func DecompressReference(reference file) =
    exec(image := ubuntu, mem := 32*GiB) (fasta file, gtf file) {"
        mkdir /tmp/reference
        tar -C /tmp/reference -xzvf {{reference}} 
        ls -lha 
        cp /tmp/reference/*fa {{fasta}}
        cp /tmp/reference/*gtf {{gtf}}
    "}

func starIndex(fasta, gtf file) (out dir) =
    exec(image := star, mem := 32*GiB) (out dir) {"
        STAR \
        --runThreadN 12 \
        --runMode genomeGenerate \
        --genomeDir {{out}} \
        --genomeFastaFiles {{fasta}} \
        --sjdbGTFfile {{gtf}}
    "}

func starAlign(read1, read2 [file], genomeDir dir) = 
    exec(image := star, mem := 12*GiB) (out dir) {"
        cd {{out}}
        STAR --outFilterType BySJout \
            --outFilterMultimapNmax 20 \
            --alignSJoverhangMin 8 \
            --alignSJDBoverhangMin 1 \
            --outFilterMismatchNmax 999 \
            --outFilterMismatchNoverLmax 0.04 \
            --alignIntronMin 20 \
            --alignIntronMax 1000000 \
            --alignMatesGapMax 1000000 \
            --outSAMstrandField intronMotif \
            --outSAMtype BAM Unsorted \
            --outSAMattributes NH HI NM MD \
            --outReadsUnmapped Fastx \
            --readFilesCommand zcat \
            --runThreadN 12 \
            --genomeDir {{genomeDir}} \
            --readFilesIn {{read1}} {{read2}}
    "}

func samSort(aligned file) =
    exec (image := samtools, mem := GiB) (sorted file) {"
        samtools sort -m 6000000000 -o {{sorted}} {{aligned}}
    "}

func samIndex(sorted file) =
    exec(image := samtools, mem := GiB) (index file) {"
        samtools index -b {{sorted}} {{index}}
    "}

func samSortByName(sorted file) =
    exec(image := samtools, mem := GiB) (byname file) {"
        samtools sort -m 5000000000 -n -o {{byname}} {{sorted}}
    "}

func htseqCount(byName, gtf file) =
   exec(image := htseq, mem := GiB) (counts file) {"
     htseq-count -r name -s no -f bam -m intersection-nonempty {{byName}} {{gtf}} > {{counts}}
   "}

val Main = {
    read1 := "s3://czbiohub-maca/remux_data/170907_A00111_0051_BH2HWLDMXX/rawdata/N8-MAA000612-3_9_M-1-1_S36/N8-MAA000612-3_9_M-1-1_S36_R1_001.fastq.gz"
    read2 := "s3://czbiohub-maca/remux_data/170907_A00111_0051_BH2HWLDMXX/rawdata/N8-MAA000612-3_9_M-1-1_S36/N8-MAA000612-3_9_M-1-1_S36_R2_001.fastq.gz"
    name := "G1-D042475-3_9_M-1-1_S236"
    output := "s3://olgabot-maca/star_htseq_test/"
    genome := "mm10-plus"


    r1 := util.SplitByPipe(read1)
    r2 := util.SplitByPipe(read2)

    reference := file("s3://czbiohub-reference/" + genome + ".tgz")
    val (fasta, gtf) = DecompressReference(reference)

    val index = starIndex(fasta, gtf)
    val alignment_output = starAlign(r1, r2, index)
    val (aligned, _) = dirs.Pick(alignment_output, "Aligned.out.bam")
    val sorted = samSort(aligned)
    val index = samIndex(sorted)
    val byName = samSortByName(sorted)
    val counts = htseqCount(byName, gtf)

    count_output := output + "htseq-count.txt"
    files.Copy(counts, count_output)
    // dirs.Copy(alignment_output, output)
}
