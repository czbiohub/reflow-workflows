// Module with generic file utilities

val image = "ubuntu:16.04"
val cpu = 1
val mem = GiB

// Gzip gzips a file
//
// @param f File to be gzipped
// @return gzipped Gzipped version of the input file f
func Gzip(f file) =
	exec(image, cpu, mem) (gzipped file) {"
		gzip -c {{f}} > {{gzipped}}
	"}

// Gunzip gzips a file
//
// @param gz File to be unzipped
// @return f Unzipped file
func Gunzip(gz file) =
	exec(image, cpu, mem) (f file) {"
		gunzip -c {{gz}} > {{f}}
	"}

// Echo creates a file with a given string
//
// @param towrite String to write to file
// @return output Newly created file
func Echo(towrite string) =
	exec(image, cpu, mem) (output file) {"
		printf "{{towrite}}" > {{output}}
	"}

// EmptyFile is a file of size 0.
val EmptyFile = file("/dev/null")

// Concat concatenates text files together using cat.
func Concat(files [file]) =
	if (len(files) == 1) {
		val [returnFile] = files
		returnFile
	} else {
		exec(image, cpu, mem) (output file) {"
			cat {{files}} > {{output}}
		"}
	}

// Header extracts the first line of the file into a new file
func Header(f file) =
	exec(image := image, cpu, mem) (result file) {"
		head -n1 {{f}} > {{result}}
	"}

// HeaderLess extracts all but the first line of the file into a new file
func HeaderLess(f file) =
	exec(image := image, cpu, mem) (result file) {"
		tail -n+2 {{f}} > {{result}}
	"}

// LineCount Counts the number of lines in a file
func LineCount(f file) =
	exec(image := image, cpu, mem) (result file) {"
		wc -l {{f}} > {{result}}
	"}